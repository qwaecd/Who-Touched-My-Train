plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev.legacyforge' version '2.0.99'
    id "com.github.johnrengelman.shadow" version "8.1.0"
}

legacyForge {
    version = "${minecraft_version}-${forge_version}"
    parchment {
        // Get versions from https://parchmentmc.org/docs/getting-started
        // Omit the "v"-prefix in mappingsVersion
        minecraftVersion = minecraft_version
        mappingsVersion = mapping_version
    }
    // Validate AT files and raise errors when they have invalid targets
    // This option is false by default, but turning it on is recommended
    validateAccessTransformers = true
    interfaceInjectionData.from "interfaces.json"

    interfaceInjectionData {
        publish file("interfaces.json")
    }

    runs {
        client {
            client()
            programArguments.addAll("--width", "1920", "--height", "1080", "--username", "dev")
        }
        client2 {
            client()
            programArguments.addAll("--width", "1280", "--height", "720", "--username", "player2")
        }
        data {
            data()
        }
        server {
            server()
            programArguments.addAll("--nogui")
        }
    }

    mods {
        testproject {
            sourceSet sourceSets.main
        }
    }
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

mixin {
    add sourceSets.main, "who_touched_my_train.refmap.json"
    config 'who_touched_my_train.mixins.json'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"


// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    // Put repositories for dependencies here
    // ForgeGradle automatically adds the Forge maven and Maven Central for you

    maven {
        name = 'tterrag maven'
        url = 'https://maven.tterrag.com/'
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven {
        url "https://cursemaven.com"
    }

    maven { url = 'https://maven.parchmentmc.org' }


    // If you have mod jar dependencies in ./libs, you can declare them as a repository like so.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html#sub:flat_dir_resolver
    // flatDir {
    //     dir 'libs'
    // }
}

dependencies {
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.0"))

    jarJar(implementation("io.github.llamalad7:mixinextras-forge:0.5.0")) {
        version {
            // The version range your mod is actually compatible with.
            // Note that you may receive a *lower* version than your preferred if another
            // Mod is only compatible up to 1.7.24, for example, your mod might get 1.7.24 at runtime.
            strictly '[0.5.0,)'
        }
    }

    // mod dependencies
    modImplementation ("com.simibubi.create:create-${create_minecraft_version}:${create_version}:slim") { transitive = false }
    modImplementation ("com.jozufozu.flywheel:flywheel-forge-${flywheel_minecraft_version}:${flywheel_version}")
    modImplementation ("com.tterrag.registrate:Registrate:${registrate_version}")

    modCompileOnly(("mezz.jei:jei-${jei_minecraft_version}-common-api:${jei_version}"))
    modCompileOnly(("mezz.jei:jei-${jei_minecraft_version}-forge-api:${jei_version}"))

    // create-steam-n-rails
    modRuntimeOnly ("curse.maven:create-steam-n-rails-688231:5840017")

    // jei-1.20.1-forge-15.20.0.112
    modRuntimeOnly ("curse.maven:jei-238222:6600311")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
// When "copyIdeResources" is enabled, this will also run before the game launches in IDE environments.
// See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
tasks.named('processResources', ProcessResources).configure {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)

    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            forge_version          : forge_version,
            forge_version_range    : forge_version_range,
            loader_version_range   : loader_version_range,
//            create_version_range   : create_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

// Example for how to get properties into the manifest for reading at runtime.
tasks.named('jar', Jar).configure {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)

    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "who_touched_my_train.mixins.json"
        ])
    }

    // This is the preferred method to reobfuscate your jar file
    finalizedBy 'reobfJar'
}

// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing:
// tasks.named('publish').configure {
//     dependsOn 'reobfJar'
// }

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'       // 源码与注释读取编码
    options.docEncoding = 'UTF-8'    // 生成文档文件编码
    options.charSet = 'UTF-8'        // HTML meta charset
    options.addStringOption('Xdoclint:none', '-quiet')
}


wrapper {
    gradleVersion = '8.4'
    distributionType = Wrapper.DistributionType.BIN
}
